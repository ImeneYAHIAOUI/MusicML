MusicPiece:
  ('Player' player=STRING)?
  'MusicPiece' name=ID
  ('Author' composer=STRING)?
  ('TicksPerQuarterNote' ticksPerQuarterNote=INT)?
  'DefaultTempo' defaultTempo=INT 'bpm'
  ('Tempos'
  tempos*=Tempo)?
  'DefaultTimeSignature' defaultTimeSignature=DefaultTimeSignature
  ('TimeSignatures'
  timeSignatures*=TimeSignature)?
  tracks+=Track
;

Tempo:
  value=INT 'bpm' position=Position
;

TimeSignature:
  numerator=INT '/' denominator=INT  bar=INT
;

DefaultTimeSignature:
  numerator=INT '/' denominator=INT
;

Track:
    'Track' name=ID 'Instrument' instrument=STRING ('Channel' channel=INT)? ('velocity' velocity=INT)?
     controlMessages*=ControlMessage
     '|' bars+=BaseBar
     midiRegions*=MidiRegion
;

ControlMessage:
    'CC:' '(' (CC=INT | message=STRING) ',' value=INT ','  position=Position ')'
;

BaseBar:
    Bar | ReusedBar | EmptyBar
;

Bar:
     'Bar' name=ID '->'  ('v:' velocity=INT ',')? musicalEvents+=MusicalEvent  '|'
;

ReusedBar:
     'ReuseBar' ref=ID (times=INT 'times')? ('->')? ('v:' velocity=INT ',')? ('a:' '[' musicalEvents*=MusicalEvent ']')? ('r:' '[' removedNotes*=RemoveEvent ']')?  ( 'c:' '[' changedEvents*=ChangeEvent ']')?  '|'
;

RemoveEvent:
      value=STRING ',' position=NotePosition
;


ChangeEvent:
    '(' NotePosition ',' value=STRING  ')' '-->'  to=Note
;
EmptyBar:
    'EmptyBar' (times=INT 'times')? '|'
;

MusicalEvent:
    Note | Rest | Chord
;



Note:
    '('  ('rep:'  repeat=INT ',')? values*=STRING (',' 'p:' position=NotePosition)? ','   ('d:')? duration=MidiDuration (',' velocity=INT)? ')' (',')?
;

Chord:
    'Chord' '('  ('rep:'  repeat=INT ',')? notes+=Note (',' 'p:' position=NotePosition)?  (',' velocity=INT)? ')' (',')?
;



MidiDuration:
    predefined=DurationValue |  'custom:'userDefined=MidiTime
;

MidiTime:
    Tick | BeatFraction
;

Tick:
    'T' value=INT
;


BeatFraction:
     numerator=INT '/' denominator=INT
;

DurationValue:
    'whole' |
    '2-1/2' | 't' | '1/2' | '2-1/4' | '3-1/2' | '1/4' |
    '2-1/8'  | '1/8' | '2-1/16' | '3-1/8' | '1/16' |
    '2-1/32' | '3-1/16' | '1/32' | '2-1/64' | '3-1/32' | '1/64' |
    '2-1/128' | '3-1/64' | '1/128' | '3-1/4'
;


Position:
    '(' bar=INT',' beat=INT',' offset=MidiTime ')'
;

NotePosition:
    beat=INT | '(' beat=INT ',' offset=MidiTime ')'
;



Rest:
    '(' 'Rest' ('p:' position=NotePosition',')?  duration=MidiDuration ')' (',')?
;

MidiRegion:
    Region | ReusedRegion
;

Region:
    'Region' name=ID '->' '(' position=Position  ',' 'duration:'duration=MidiDuration ')'
;

ReusedRegion:
    'ReuseRegion' ref=ID '->' '('position=Position  ( ',' velocity=INT ',')? musicalEvents*=MusicalEvent ')'
;

Comment:
    /\/\/.*$/
;