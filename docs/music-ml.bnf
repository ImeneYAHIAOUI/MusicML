<MusicPiece> ::= ("Live" <STRING>)?
     		 "MusicPiece" <STRING>
     		 ("Author" <STRING>)?
     		 ("HumanError" <INT> "notes" <INT> "ms")?
     		 ("TicksPerQuarterNote" <INT>)?
     		 "DefaultTempo" <INT> "bpm"
     		 ("Tempos" <Tempo>*)?
     		 "DefaultTimeSignature" <INT> "/" <INT>
     		 ("TimeSignatures" <TimeSignature>*)?
     		 <SongDefinition>

<Tempo> ::= <INT> "bpm" <Position>

<TimeSignature> ::= <INT> "/" <INT> <INT>

<SongDefinition> ::= <Track>+ | <Sections>

<Track> ::= "Track" <STRING>
   		 "Instrument" <STRING>
   		 ("Channel" <INT>)?
   		 <ControlMessage>*
   		 <ContentDefinition>

<Sections> ::= "tracks :" <TrackDef>+
   			"sections :" <Section>+
   			"arrangement :" <STRING>+

<TrackDef> ::= "Track" <STRING>
   			"Instrument" <STRING>
   			 ("Channel" <INT>)?

<Section> ::= "Section" <STRING> ":"
   			 <SectionTrack>+

<SectionTrack> ::= "Track" <STRING>
   				 <ControlMessage>*
   				 <ContentDefinition>

<ContentDefinition> ::= <PlayableBars> | <ArrangedBars>

<PlayableBars> ::= "|" <BaseBar>+

<ArrangedBars> ::= "bars :"
   				"|" <BaseBar>+
   				"regions :" <MidiRegion>+
   				"arrangement :" <Arrangement>+

<Arrangement> ::= <ArrangedBar> | <ArrangedRegion>

<ArrangedBar> ::= "bar" <STRING> (",")?

<ArrangedRegion> ::= "region" <STRING> (<RegionStart>)? (",")?

<RegionStart> ::= <NegativeNotePosition> | <NotePosition>

<ControlMessage> ::= "CC :" "(" (<INT> | <STRING>) "," <INT> "," <Position> ")"

<BaseBar> ::= <Bar> | <ReusedBar> | <EmptyBar>

<Bar> ::= "Bar" <STRING> "->" <MusicalEvent>+ "|"

<ReusedBar> ::= "ReuseBar" <STRING> "use:" <STRING> (<INT> "times")?
   			 ( "->" ("a:" "[" <MusicalEvent>+ "]")? ("r:" "[" <RemoveEvent>+ "]")? ("c:" "[" <ChangeEvent>+ "]")? )? "|"

<RemoveEvent> ::= "(" <STRING>* "," <NotePosition> ")" (",")?

<ChangeEvent> ::= "(" <STRING> "," <NotePosition> ")" "-->" <STRING> (",")?

<EmptyBar> ::= "EmptyBar" (<INT> "times")? "|"

<MusicalEvent> ::= <Note> | <Rest> | <Chord>

<Note> ::= "(" ("rep:" <INT> ",")? <STRING>+ ("," "p:" <NotePosition>)? "," ("d:")? <MidiDuration> ("," "v:"  <INT>)? ")" (",")?

<Chord> ::= "Chord" "(" ("rep:" <INT> ",")? <Note>+ ("," "p:" <NotePosition>)? ("," "v:" <INT>)? ")" (",")?

<MidiDuration> ::= <DurationValue> | "custom:" <MidiTime>

<MidiTime> ::= <Tick> | <BeatFraction>

<Tick> ::= "T" <INT>

<BeatFraction> ::= <INT> "/" <INT>

<DurationValue> ::= "whole" | "2-1/2" | "t" | "1/2" | "2-1/4" | "3-1/2" | "1/4" | "2-1/8" | "1/8" | "2-1/16" | "3-1/8" | "1/16" | "2-1/32" | "3-1/16" | "1/32" | "2-1/64" | "3-1/32" | "1/64" | "2-1/128" | "3-1/64" | "1/128" | "3-1/4"

<Position> ::= "(" <INT> "," <INT> "," <MidiTime> ")"

<NotePosition> ::= <INT> | "(" <INT> "," <MidiTime> ")"

<NegativeNotePosition> ::= "-" <NotePosition>

<Rest> ::= "(" "Rest" ("p:" <NotePosition> ",")? <MidiDuration> ")" (",")?

<MidiRegion> ::= "Region" <STRING> "->" "(" <Position> "," <Position> ")"

<STRING> ::= ( [a-z] | [A-Z] | [0-9] | "_" )+

<INT> ::= [0-9]+

<Comment> ::= "//" <STRING>