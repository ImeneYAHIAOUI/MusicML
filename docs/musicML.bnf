<MusicPiece> ::= "MusicPiece" <ID> 
                ("Author" <STRING>)?
                ("TicksPerQuarterNote" <INT>)?
                "DefaultTempo" <INT>
                "Tempos" <Tempo>*
                "DefaultTimeSignature" <TimeSignature>
                "TimeSignatures" <BarTimeSignature>*
                "Tracks" <Track>+

<Tempo> ::= <INT> "bpm" "(" <ID> ("," <MidiDuration>)? ")"

<DefaultTimeSignature> ::= <FRACTION>

<BarTimeSignature> ::= <FRACTION> <INT>

<Track> ::= 'Track' name=<ID> 'Instrument' <STRING> 
   ('Channel' <INT>)? 
   ('velocity' <INT>)? 
   <ControlMessage>* 
   '|' <BaseBar>+
   <MidiRegion>

<ControlMessage>: 'CC:' '(' <STRING> ',' <INT> ',' <INT> (',' <MidiDuration>)? ')'

<BaseBar>::= <Bar> | <ReusedBar> | <EmptyBar>

<Bar>::= 'Bar' <ID> '->'  ('vol:' <INT> ',')? <MusicalEvent>+ ('overlap:' <MusicalEvent>+)? '|'

<ReusedBar>::= 'ReuseBar' <ID> (<INT> 'times')? ('->' ('vol:' <INT> ',')? <MusicalEvent>*  ('overlap:' <MusicalEvent>*)? )? '|'

<EmptyBar>::= 'EmptyBar' (<INT> 'times')? '|'

<MusicalEvent>::= <Note> | <Rest>

<Note>::= <SimpleNote> | <Chord>

<SimpleNote>::= '(' ('rep:' <INT> ',')? <STRING>+ ',' 
                     ( ('start:')? <MidiDuration> ',')?  
                     ('duration:')? <MidiDuration> (',' <INT>)? 
                ')' 
                (',')?

<MidiDuration> ::= "T" <INT> | <FRACTION> | <DurationValue>

<DurationValue> ::= 'whole' | '2-1/2' | 't' | '1/2' | '2-1/4' | '3-1/2' | '1/4' | '2-1/8'  | '1/8' | '2-1/16' | '3-1/8' | '1/16' | '2-1/32' | '3-1/16' | '1/32' | '2-1/64' | '3-1/32' | '1/64' | '2-1/128' | '3-1/64' | '1/128' | '3-1/4'

<Chord>::= 'Chord' '(' ('rep:'  <INT> ',')? ( 'vol:' <INT> ',')?  <Note>+ ')' (',')?

<Rest>:='(' 'Rest' ('pos:' <MidiDuration>',')?  <MidiDuration> ')' (',')?

<MidiRegion>::= <Region> | <ReusedRegion>

<Region>::= 'Region' <ID> '->' '('  <INT> (',start:' <MidiDuration>)? ',' 'size:' <MidiDuration> ')'

<ReusedRegion>::= 'ReuseRegion' <ID> '->' '(' <INT> (',' <MidiDuration>)? ( ',' <INT> ',')? <MusicalEvent>* ')'

<Comment>::= '//' <STRING>+

<ID> ::= ( <letter> | <digit> | <character> )+

<INT> ::= <digit>+

<FRACTION> ::= <INT> '/' <INT>

<STRING> ::= (<letter> | <space> | <digit> | <character> )+

<letter> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<character> ::= "_" | "-" | "#" | "~"

<space> ::= " "
