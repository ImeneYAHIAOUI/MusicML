MusicPiece:
  'MusicPiece' name=ID
  ('Author' composer=STRING)?
  ('TicksPerQuarterNote' ticksPerQuarterNote=INT)?
  'DefaultTempo' defaultTempo=INT 'bpm'
  ('Tempos'
  tempos*=Tempo)?
  'DefaultTimeSignature' defaultTimeSignature=DefaultTimeSignature
  ('TimeSignatures'
  timeSignatures*=TimeSignature)?
   tracks+=Track
;
Tempo:
  value=INT 'bpm' '('bar=INT (',' start=MidiDuration)? ')'
;
TimeSignature:
  numerator=INT '/' denominator=INT  bar=INT
;
DefaultTimeSignature:
  numerator=INT '/' denominator=INT
  ;
Track:
    'Track' name=ID 'Instrument' instrument=STRING ('Channel' channel=INT)? ('velocity' velocity=INT)?
     controlMessages*=ControlMessage
     '|' bars+=BaseBar
     midiRegions*=MidiRegion
;
ControlMessage:
    'CC:' '(' (CC=INT | message=STRING) ',' value=INT ','  bar=INT (',' start=MidiDuration)? ')'
;
BaseBar:
    Bar | ReusedBar | EmptyBar
;
Bar:
     'Bar' name=ID '->'  ('vol:' velocity=INT ',')? musicalEvents+=MusicalEvent ( 'overlap:' overlappingEvents*=MusicalEvent)? '|'

;


ReusedBar:
     'ReuseBar' ref=ID (times=INT 'times')? ('->')? ('vol:' velocity=INT ',')? musicalEvents*=MusicalEvent  ('overlap:' overlappingEvents*=MusicalEvent)? '|'
;

EmptyBar:
    'EmptyBar' (times=INT 'times')? '|'
;

MusicalEvent:
    Note | Rest
;




Note:
    SimpleNote | Chord
;
SimpleNote:
    '('  ('rep:'  repeat=INT ',')? values*=STRING ',' ( ('start:')? start=MidiDuration ',')?  ('duration:')? duration=MidiDuration (',' velocity=INT)? ')' (',')?
;
MidiDuration:
    'T' value=INT?
    | fraction=Fraction
    | durationValue=DurationValue
;

Fraction:
   'F' numerator=INT '/' denominator=INT
;
DurationValue:
    'whole' |
    '2-1/2' | 't' | '1/2' | '2-1/4' | '3-1/2' | '1/4' |
    '2-1/8'  | '1/8' | '2-1/16' | '3-1/8' | '1/16' |
    '2-1/32' | '3-1/16' | '1/32' | '2-1/64' | '3-1/32' | '1/64' |
    '2-1/128' | '3-1/64' | '1/128' | '3-1/4'
;
Chord:
    'Chord' '(' ('rep:'  repeat=INT ',')? ( 'vol:' velocity=INT ',')?  notes+=Note ')' (',')?
;
Rest:
    '(' 'Rest' ('pos:' start=MidiDuration',')?  duration=MidiDuration ')' (',')?
;
MidiRegion:
    Region | ReusedRegion
;
Region:
    'Region' name=ID '->' '('  bar=INT (',start:' start=MidiDuration)? ',' 'size:'size=MidiDuration ')'
;
ReusedRegion:
    'ReuseRegion' ref=ID '->' '('bar=INT (',' start=MidiDuration)? ( ',' velocity=INT ',')? musicalEvents*=MusicalEvent ')'
;


Comment:
    /\/\/.*$/
;