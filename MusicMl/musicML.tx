MusicPiece:
  'MusicPiece' name=ID
  ('Author' composer=STRING)?
  ('TicksPerQuarterNote' ticksPerQuarterNote=INT)?
  'DefaultTempo' defaultTempo=INT 'bpm'
  ('Tempos'
  tempos*=Tempo)?
  'DefaultTimeSignature' defaultTimeSignature=DefaultTimeSignature
  ('TimeSignatures'
  timeSignatures*=TimeSignature)?
   tracks+=Track
;
Tempo:
  value=INT 'bpm' 'position' position=INT
;
TimeSignature:
  numerator=INT '/' denominator=INT 'position' position=INT
;
DefaultTimeSignature:
  numerator=INT '/' denominator=INT
  ;
Track:
    'Track' name=ID 'Instrument' instrument=STRING ('Channel' channel=INT)? ('velocity' velocity=INT)?
     controlMessages*=ControlMessage
     '|' bars+=BaseBar
     midiRegion*=MidiRegion
;
ControlMessage:
    'CC' CC=INT value=INT
;
BaseBar:
    Bar | ReusedBar
;
Bar:
     'Bar' name=ID ':'  position=INT (',')? ('velocity' velocity=INT ',')? musicalEvents*=MusicalEvent '|'

;
ReusedBar:
     'ReusedBar' ref=ID (times=INT 'times')? ':' position=INT (',')? ('velocity' velocity=INT ',')? musicalEvents*=MusicalEvent '|'
;
MusicalEvent:
    Note | Rest
;
Note:
    SimpleNote | Chord
;
SimpleNote:
    '(' values*=STRING ',' ( start=MidiDuration ',')?  duration=MidiDuration (',' velocity=INT)? ')' (',')?
;
MidiDuration:
    'T' value=INT? |
    durationValue=DurationValue
;

DurationValue:
    'whole' |
    '1/2 dotted' | 'triplet' | '1/2' | '1/4 dotted' | '1/2 triplet' | '1/4' |
    '1/8 dotted' | '1/4 triplet' | '1/8' | '1/16 dotted' | '1/8 triplet' | '1/16' |
    '1/32 dotted' | '1/16 triplet' | '1/32' | '1/64 dotted' | '1/32 triplet' | '1/64' |
    '1/128 dotted' | '1/64 triplet' | '1/128'
;
Chord:
    'Chord' '(' ('rep:'  repeat=INT ',')? ( 'vol:' velocity=INT ',')?  notes+=Note ')' (',')?
;
Rest:
    '(' 'Rest' ('pos:' start=MidiDuration',')?  duration=MidiDuration ')' (',')?
;
MidiRegion:
    Region | ReusedRegion
;
Region:
    'Region' name=ID '{' start=MidiDuration ',' size=MidiDuration (',' velocity=INT ',')? musicalEvents+=MusicalEvent '}'
;
ReusedRegion:
    'ReusedRegion' ref=ID '{' start=MidiDuration ',' size=MidiDuration ( ',' velocity=INT ',')? musicalEvents+=MusicalEvent '}'
;


Comment:
    /\/\/.*$/
;